apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.2'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 8
        versionName '0.2.0'
    }
}

dependencies {
    implementation "androidx.annotation:annotation:${rootProject.ext.androidxVersion}"
    implementation "androidx.fragment:fragment:${rootProject.ext.androidxVersion}"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.2.22'
    testImplementation 'org.robolectric:robolectric:4.2'
}

task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

nexusStaging {
    packageGroup = 'com.braintreepayments'
}

def sonatypeUsername = System.env['SONATYPE_USERNAME']
def sonatypePassword = System.env['SONATYPE_PASSWORD']

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.setArtifactId 'browser-switch'

            pom.project {
                name 'browser-switch'
                groupId = 'com.braintreepayments'
                version = "${android.defaultConfig.versionName}"
                packaging 'aar'
                description 'A library making it easy to open a url in a browser or Chrome Custom Tab and receive a response from the web page.'

                url 'https://github.com/braintree/browser-switch-android'

                scm {
                    url 'scm:git@github.com:braintree/browser-switch-android.git'
                    connection 'scm:git@github.com:braintree/browser-switch-android.git'
                    developerConnection 'scm:git@github.com:braintree/browser-switch-android.git'
                }

                developers {
                    developer {
                        id 'devs'
                        name 'Braintree Payments'
                    }
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }
            }

            pom.whenConfigured { MavenPom pom ->
                pom.dependencies.each {
                    if (it.artifactId == 'support-annotations') {
                        it.optional = true
                    }
                }
            }
        }
    }
}
